buildscript {
    ext {
        kotlinVer = '1.2.61'
        springBootVer = '2.0.4.RELEASE'
        kotlinLoggingVer = '1.6.10'

        jimfsVer = '1.1'

        propdepsPluginVer = '0.0.10.RELEASE'
        versionPluginVer = '0.20.0'
    }

    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVer"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVer"
        classpath "io.spring.gradle:propdeps-plugin:$propdepsPluginVer"
        // gradle dependencyUpdates -Drevision=release
        classpath "com.github.ben-manes:gradle-versions-plugin:$versionPluginVer"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'propdeps'
apply plugin: 'propdeps-idea'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVer"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVer"

    compile "io.github.microutils:kotlin-logging:$kotlinLoggingVer"

    compile 'com.fasterxml.jackson.module:jackson-module-kotlin'

    compile 'org.springframework.boot:spring-boot-starter-webflux'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile "com.google.jimfs:jimfs:$jimfsVer"

    testCompile "org.jetbrains.kotlin:kotlin-test-junit5:$kotlinVer"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

group = 'app'
version = '0.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10

tasks.withType(JavaCompile) {
    options.fork = true
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        apiVersion = "1.2"
        languageVersion = "1.2"
        jvmTarget = "1.8"
    }
}

bootJar {
    mustRunAfter tasks.clean
    mainClassName = 'app.AppKt'
    archiveName 'app.jar'
}

tasks.withType(Test) {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

wrapper {
    gradleVersion = '4.10'
    distributionType = Wrapper.DistributionType.ALL
}
